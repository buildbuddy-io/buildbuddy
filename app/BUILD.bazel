load("@npm_bazel_typescript//:index.bzl", "ts_devserver", "ts_library")
load("@npm_bazel_rollup//:index.bzl", "rollup_bundle")
load("@npm_bazel_terser//:index.bzl", "terser_minified")

package(default_visibility = ["//visibility:public"])

rollup_bundle(
    name = "app_bundle",
    config_file = "rollup.config.js",
    entry_point = "app.tsx",
    deps = [
        ":app",
        "@npm//rollup-plugin-commonjs",
        "@npm//rollup-plugin-node-resolve",
        "@npm//rollup-plugin-replace",
    ],
)

terser_minified(
    name = "app_bundle.min",
    src = ":app_bundle",
)

config_setting(
    name = "minified_js",
    values = {"compilation_mode": "opt"},
)

alias(
    name = "app_js_entry_point",
    actual = select({
        ":minified_js": ":app_bundle.min",
        "//conditions:default": ":app_bundle",
    }),
)

ts_library(
    name = "app",
    srcs = glob(["*.tsx"]),
    deps = [
        "//app/root",
        "@npm//@types/react",
        "@npm//@types/react-dom",
        "@npm//react",
        "@npm//react-dom",
    ],
)

genrule(
    name = "style",
    srcs = [
        "//app/root:root.css",
    ],
    outs = ["style.css"],
    cmd_bash = ("""
        touch out;
        for f in $(SRCS); do
            if [[ ! -d $$f ]]; then
            (cat $$f; echo) >> out;
            fi;
        done;
        cat out > $@;
    """),
)
